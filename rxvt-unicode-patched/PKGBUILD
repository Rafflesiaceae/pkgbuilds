# Maintainer: Rafflesiaceae <rafflesiaceae.plant@gmail.com>
# Contributor: Daniel Hahler <archlinux+aur@thequod.de>
# Contributor: Florian Bruhin (The-Compiler) <archlinux.org@the-compiler.org>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: SÃ©bastien Luttringer <seblu@aur.archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org

_pkgname=rxvt-unicode
pkgname=rxvt-unicode-patched
pkgver=9.30
pkgrel=1
pkgdesc='Unicode enabled rxvt-clone terminal emulator (urxvt) with fixed font spacing'
arch=('i686' 'x86_64')
url='http://software.schmorp.de/pkg/rxvt-unicode.html'
license=('GPL')
depends=('libxft' 'perl' 'startup-notification' 'rxvt-unicode-terminfo' 'libnsl' 'libptytty')
makedepends=('libxt')
provides=(rxvt-unicode)
conflicts=(rxvt-unicode)
source=(http://dist.schmorp.de/rxvt-unicode/Attic/$_pkgname-$pkgver.tar.bz2
        'font-width-fix.patch'
        'line-spacing-fix.patch'
        'open-buffer-in-editor.pl'
        )
sha1sums=('700265a255eedf0f553cadfe5484bf71f8fb74c2'
          '01ee8f212add79a158dcd4ed78d0ea1324bdc59b'
          'b7fde1c46af45e831828738874f14b092b1e795f'
          'SKIP'
          )

prepare() {
  cd $_pkgname-$pkgver

  patch -p0 -i ../font-width-fix.patch
  patch -p0 -i ../line-spacing-fix.patch
}

build() {
  cd $_pkgname-$pkgver

  # do not specify --with-terminfo (FS#46424)
  CXXFLAGS=-std=c++11 \
  ./configure \
    --prefix=/usr \
    --enable-256-color \
    --enable-combining \
    --enable-fading \
    --enable-font-styles \
    --enable-iso14755 \
    --enable-keepscrolling \
    --enable-lastlog \
    --enable-mousewheel \
    --enable-next-scroll \
    --enable-perl \
    --enable-pointer-blank \
    --enable-rxvt-scroll \
    --enable-selectionscrolling \
    --enable-slipwheeling \
    --disable-smart-resize \
    --enable-startup-notification \
    --enable-transparency \
    --enable-unicode3 \
    --enable-utmp \
    --enable-wtmp \
    --enable-xft \
    --enable-xim \
    --enable-xterm-scroll

  # disable terminfo installation (workaround FS#79143)
  sed -Eie '/(^[[:blank:]]+\/usr\/bin\/tic)/s//#&/g' doc/Makefile

  make
}

package() {
  cd $_pkgname-$pkgver

  install -D -m 644 "${startdir}/open-buffer-in-editor.pl" \
      "${pkgdir}/usr/lib/urxvt/perl/open-buffer-in-editor"

  make DESTDIR="$pkgdir" install
}
