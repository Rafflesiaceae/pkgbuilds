# Maintainer: Rafflesiaceae <rafflesiaceae.plant@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Thomas Weißschuh <thomas t-8ch de>

pkgname=git-review
pkgver=2.3.1
pkgrel=2
pkgdesc='Tool to submit code to Gerrit'
arch=('any')
url='https://opendev.org/opendev/git-review'
license=('APACHE')
depends=('git' 'python-requests')
makedepends=('python-pbr')
checkdepends=('java-runtime' 'libcups' 'openssh' 'procps-ng' 'python-stestr' 'python-mock')
source=("git+https://opendev.org/opendev/git-review.git#tag=${pkgver}"
        https://tarballs.openstack.org/ci/gerrit/gerrit-v2.11.4.13.cb9800e.war
        download-include-tracking-branch.patch)
sha512sums=(SKIP
            '764388dc0ee381e2f05f5aaef9fc4156b4659a329eaf815ad7beb0b2a924a8d171444b8824ea9aad6b8aa7a3cc0b60bf8daa9d483298e6226cb692ea1caafa7f'
            SKIP)

prepare() {
  export PBR_VERSION=$pkgver
  mkdir -p $pkgname/.gerrit
  cp gerrit-v2.11.4.13.cb9800e.war $pkgname/.gerrit/

  # cd $pkgname-$pkgver
  cd $pkgname

  # include the tracking branch in the name of the local branch when downloading a change
  patch -s -p0 < ../download-include-tracking-branch.patch

  # disable automatic hook installation (in case of broken remote gerrit hooks)
  # sed -ri "s/(\s+)have_hook = (os.path.exists\(hook_file\) and os.access\(hook_file, os.X_OK\).*)/\1have_hook = (\"GIT_REVIEW_ALLOW_HOOK\" in os.environ) and \2/" git_review/cmd.py
    sed -ri "s/(\s+)if (not have_hook:)/\1if (\"GIT_REVIEW_ALLOW_HOOK\" in os.environ) and \2/" git_review/cmd.py

  # Remove the su - part
  sed -i '/f.write(GOLDEN_SITE_VER)/a \        utils.run_cmd("sed", "-i", "s/su - $GERRIT_USER -s//", self._dir("gsite", "bin", "gerrit.sh"))' git_review/tests/__init__.py

  # gerrit doesn't work without some additional config :/
  sed -i '/listenUrl/a [gc]\n    interval = 2d\n    startTime = Fri 12:00\n[gitweb]\n    cgi = /usr/share/gitweb/gitweb.cgi' git_review/tests/utils.py

  # git version differences?
  sed -e "s/'Branch test_branch set up to track remote'/\"Branch 'test_branch' set up to track remote\"/" \
      -e "s/' branch maint from origin.'/\" branch 'maint' from 'origin'.\"/" \
      -i git_review/tests/test_git_review.py
}

build() {
  cd $pkgname
  python setup.py build
}

package() {
  cd $pkgname
  python setup.py install --optimize=1 --root="$pkgdir"
  install -Dm644 git-review.1 "$pkgdir"/usr/share/man/man1/git-review.1
}
