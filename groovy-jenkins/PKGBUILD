# Maintainer: Rafflesiaceae <rafflesiaceae.plant@gmail.com>
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Musikolo <musikolo@hotmail.com>
# Contributor: Ruben Fonseca <fonseka@gmail.com>
# Contributor: Andreas W. Hauser <andy-aur@splashground.de>

# tracks the latest groovy version bundled within jenkins
# taken from https://github.com/archlinux/svntogit-community/blob/2505b3b362b1ff11cac6792cb3a88f63ed2fbbfd/trunk/PKGBUILD

pkgname=groovy
pkgver=2.4.12
pkgrel=1
pkgdesc='Programming language based on Java, inspired by Python, Ruby and Smalltalk'
arch=('any')
url='http://groovy-lang.org/'
license=('APACHE')
depends=('bash' 'java-environment')
optdepends=('groovy-docs: html and pdf documentation for Groovy')
makedepends=()
# .asc file is available for download, but there was no key available
source=("https://archive.apache.org/dist/$pkgname/$pkgver/distribution/apache-$pkgname-binary-$pkgver.zip")
sha256sums=('93b9a19c760c2af846afa0e9c78692d70186cdde36e070e9806fe11b84a8a7b6')

prepare() {
  # Set GROOVY_HOME correctly at the top of all the scripts.
  # There are probably better ways to do this, but this works.
  for f in "$pkgname-$pkgver/bin/"*; do
    sed 's:bin/sh:bin/sh\nGROOVY_HOME=/usr/share/groovy:' -i "$f"
  done

  cd "$pkgname-$pkgver/embeddable"
  ln -s "$pkgname-all-$pkgver.jar" "$pkgname-all.jar"
}

package() {
  cd "$pkgname-$pkgver"

  # Create the directories and package the files
  install -d "$pkgdir/usr/share/groovy" "$pkgdir/usr/bin"
  cp -r lib conf embeddable "$pkgdir/usr/share/groovy"
  cp bin/* "$pkgdir/usr/bin"

  # Remove all DOS/Windows batch files
  find $pkgdir -name '*.bat' -exec rm {} \;

  # Package the license file
  install -Dm644 LICENSE \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: groovy-lang.org/changelogs.html
# vim:set ts=2 sw=2 et:
